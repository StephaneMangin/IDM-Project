/*
 * generated by Xtext
 */
package org.istic.idm.xtext.validation

import org.eclipse.xtext.validation.Check
import org.istic.idm.xtext.videoGen.VideoGenPackage.Literals
import org.istic.idm.xtext.videoGen.Alternatives
import org.istic.idm.xtext.videoGen.Sequence
import org.istic.idm.xtext.videoGen.Optional
import org.istic.idm.xtext.videoGen.VideoGen

/**
 * This class contains custom validation rules. 
 *
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#validation
 */
class VideoGenValidator extends AbstractVideoGenValidator {

  public static val INVALID_NAME = 'invalidName'
  public static val INVALID_PROBABILITY = 'invalidProbability'

	@Check
	def checkUniqueIdentifiers(Sequence sequence) {
		sequence.eResource.allContents
			.filter(typeof(Sequence))
			.takeWhile[seq2 | !seq2.equals(sequence)]
			.forEach[seq2 |
				if (seq2.name.equals(sequence.name)) {
					error('Sequence name should be unique.', 
							Literals.SEQUENCE__NAME,
							INVALID_NAME)
				}
			]
	}
	
	@Check
	def checkOptionalProbability(Optional optional) {
		if (optional.probability > 100) {
			error('Optional probability should not be higher than 100%', 
					Literals.OPTIONAL__PROBABILITY,
					INVALID_PROBABILITY)
		}
		else if (optional.probability == 100) {
			warning('Optional probability should not equal 100%, otherwize create a Mandatory sequence instead ;)', 
					Literals.OPTIONAL__PROBABILITY,
					INVALID_PROBABILITY)
		}
	}
	
	@Check
	def checkAlternativesProbability(Alternatives alternatives) {
		var total = 0
		for (option: alternatives.options) {
			total += option.probability
		}	
		if (total > 100) {
			error('Probabilities sum inside an alternatives should not exceed 100%', 
					Literals.ALTERNATIVES__OPTIONS,
					INVALID_PROBABILITY)
		}
	}
	
}
